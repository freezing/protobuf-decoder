buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3' apply false
}

apply plugin: "idea"

defaultTasks 'clean', 'test', 'shadowJar'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    compileJava {
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    compileTestJava {
        options.encoding = 'UTF-8'
        options.deprecation = true
    }
}

project(":proto-encoding-core") {
    dependencies {
        // https://mvnrepository.com/artifact/org.agrona/agrona
        compile group: 'org.agrona', name: 'agrona', version: '1.0.1'

        // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
        compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.11.1'

        // https://mvnrepository.com/artifact/commons-codec/commons-codec
        compile group: 'commons-codec', name: 'commons-codec', version: '1.13'

        // Use 'api' rather than 'compile' for Android or java-library projects.
        compile             "com.google.auto.value:auto-value-annotations:1.6.2"
        annotationProcessor "com.google.auto.value:auto-value:1.6.2"

        compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        // https://mvnrepository.com/artifact/com.google.truth/truth
        testCompile group: 'com.google.truth', name: 'truth', version: '0.45'

        // https://mvnrepository.com/artifact/org.mockito/mockito-core
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    }

    test {
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }
}

project(':proto-encoding-benchmarks') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compile project(':proto-encoding-core')

        compile             "com.google.auto.value:auto-value-annotations:1.6.2"
        annotationProcessor "com.google.auto.value:auto-value:1.6.2"

        compile 'org.openjdk.jmh:jmh-core:1.21'
        annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.21'
    }

    shadowJar {
        archiveName = 'benchmarks.jar'
        classifier = 'benchmarks'
        manifest.attributes('Main-Class': 'org.openjdk.jmh.Main')
    }
}

task jmh(type: Exec) {
    commandLine 'java',
            '-jar', 'proto-encoding-benchmarks/build/libs/benchmarks.jar',
            "DecoderBenchmark.customProtobufObjectMerge",
            '-w', '1s', '-r', '1s', '-wi', '3', '-i', '100', '-f', '1', '-prof', 'gc'
}

wrapper {
    gradleVersion = '4.10.1'
    distributionType = 'ALL'
}